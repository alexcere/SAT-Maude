***
*** Modern DPLL inference rules.
*** Taken directly from 
*** Solving SAT and SAT Modulo Theories: from an
*** Abstract Davis-Putnam-Logemann-Loveland
*** Procedure to DPLL(T)
***

sload logic
sload uip

mod BASIC-DPLL is 
	pr LOGIC .
	pr UNIQUE-IMPLICATION-POINT .

    var p : Qid .
	var l x y : Literal .
	var M N CTX : Context .
	var C C' C'' : Clause .
	var CS : ClauseSet .

    crl [UnitPropagate] : M || (l \/ C), CS => M l || (l \/ C), CS
	    if M |= C = false /\ definedLiteral(l, M) = false .

	crl [PureLiteral] : M || (l \/ C), CS => M l || (l \/ C), CS 
	    if (~(l) in CS) = false /\ definedLiteral(l,M) = false .
 
	crl [Decide] : M || (l \/ C),CS => M d(l) || (l \/ C),CS 
	    if definedLiteral(l,M) = false .

	crl [Fail] : M || CS,C => failState if M |= C == false
	    /\ existsDecisionLiteral(M) = false .

	*** In order to guarantee that we backjump to the lowest possible
	*** level, we are separating Backjump in two rules.

	*** If we have several decision literals along the context,
	*** the lowest level we can jump to
	*** is the one in which next literal is a decision one and
	*** it is verified that the backjump clause's only free
	*** literal is the UIP.
	*** l is the negated UIP, and C' is the backjump clause.
	crl [Backjump1] : M d(x) N d(y) CTX || CS, C
	    => M d(x) N l || CS,C
	    if (M d(x) N d(y) CTX) |= C == false /\ existsDecisionLiteral(N) = false 
		/\ C'' := removeLiteralFromClause(l, C')
	    /\ M d(x) N |= C''  == false /\ M |= C'' =/= false 
		/\ definedLiteral(l, M d(x) N) = false [nonexec] .

	*** If we have to backjump to first level, we just undo first decision.
	crl [Backjump2] : M d(x) N || C,CS => M l || C,CS
	    if M d(x) N |= C == false
	    /\ existsDecisionLiteral(M) = false 
		/\ C'' := removeLiteralFromClause(l, C') 
		/\ M |= C'' == false 
		/\ definedLiteral(l, M) = false [nonexec] . 
endm

mod DPLL-WITH-LEARNING is protecting BASIC-DPLL .

    var M : Context .
    var C : Clause .
    var CS : ClauseSet .
    
    rl [Learn] : M || CS => M || CS,C [nonexec] .

    rl [Forget] : M || CS, C => M || CS [nonexec] .

endm

mod MODERN-DPLL is protecting DPLL-WITH-LEARNING .

    var M : Context .
    var CS : ClauseSet .

    rl [Restart] : M || CS => emptyCTX || CS .

endm