***
*** Strategy for applying BASIC-DPLL rules 
*** Inspired in CDCL algorithm
***

sload basic-dpll
sload uip 

smod BASIC-DPLL-STRATEGY is 
    pr DPLL-WITH-LEARNING .
    pr UNIQUE-IMPLICATION-POINT .

    var l x : Literal .
    var C C' : Clause .
    var CS CS' : ClauseSet .
    var BasicSeq : BasicSequent .
    var M CTX N : Context .
    var CIS CIS' : ConflictInfoSet .

    strat basic-dpll-strat :   @ BasicSequent .
    strats decide-if-fails propagate success-branch : ConflictInfoSet @ BasicSequent .
    strat fail-branch : Literal Clause Clause ConflictInfoSet @ BasicSequent .

    sd fail-branch(l, C, C', CIS) :=  (Backjump1[l <- l, C <- C, C' <- C'] |
        Backjump2[C <- C, l <- l, C' <- C']) ? (Learn[C <- C'] ; 
        matchrew BasicSeq s.t. (M || CS) := BasicSeq /\ CIS' := generateConflictInfoSetFromBackjump(M, CIS, C') 
        by BasicSeq using propagate(CIS')) : Fail .

    sd decide-if-fails(CIS) := matchrew BasicSeq s.t. (M || C, CS) := BasicSeq  /\ (M |= C == false)
        /\ CIS' :=  obtainCurrentConflictInfoSet(M, CIS)
        /\ C' :=  obtainBackjumpClause(CIS', C) /\ l := obtainNegatedUIP(CIS', C')  
        by BasicSeq using (fail-branch(l, C, C', CIS)) or-else success-branch(CIS) .

    sd propagate(CIS) := (matchrew BasicSeq s.t. CTX || (l \/ C), CS := BasicSeq 
        /\ CTX |= C == false /\ definedLiteral(l, CTX) = false /\  CIS' := CIS l -> (l \/ C) 
        by BasicSeq using (UnitPropagate[l <- l, C <- C]; propagate(CIS'))) or-else decide-if-fails(CIS) [label pro] .

    sd success-branch(CIS) := one(Decide) ? matchrew BasicSeq s.t. (N d(l) || CS) := BasicSeq /\ CIS' := (CIS l -> []) by BasicSeq
        using (propagate(CIS')) : idle .

    sd basic-dpll-strat := PureLiteral ! ; propagate(emptyCIS) .
     
endsm